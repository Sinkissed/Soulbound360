//Soulbound360
//by Sinkissed
//0.36
//For DirectX Driver
//Hunters & Warlock Control Set
//Key Bindings are Default

//analog mouse sensitivity set relative to screen size
//try to change the mouse sensitivity in the game first if its wrong
var.MouseSensitivity = screen.width/32

if Xinput.Exists==0 then
//Morii&non-360 controllers - swap pitch/yaw for x/y to change mouse joystick
var.MouseX = Joystick1.pitch
var.MouseY = Joystick1.yaw
var.MoveX = Joystick1.x
var.MoveY = Joystick1.y
var.MouseDeadzone = .07
var.MoveDeadzone = .05
else
//For default DirectX driver - switch Joy2 to Joy1 to change mouse
var.MouseX = Xinput1.Joy2x
var.MouseY = -Xinput1.Joy2y
var.MoveX = Xinput1.Joy1x
var.MoveY = -Xinput1.Joy1y
var.MouseDeadzone = .13
var.MoveDeadzone = .5
var.TriggerDeadzone = 0.85
endif

if Xinput.Exists==0 then
var.Vibrate1 = Joystick1.Vibration1
var.Vibrate2 = Joystick1.Vibration2
var.RB = Joystick1.Button6
Var.RT = Joystick1.Button12
Var.LB = Joystick1.Button5
Var.LT = Joystick1.Button11
Var.X = Joystick1.Button3
Var.Y = Joystick1.Button4
Var.A = Joystick1.Button1
Var.B = Joystick1.Button2
Var.Start = Joystick1.Button8
Var.Back = Joystick1.Button7
Var.DUp = Joystick1.Pov1Up
Var.DDown = Joystick1.Pov1Down
Var.DLeft = Joystick1.Pov1Left
Var.DRight = Joystick1.Pov1Right
Var.LThumb = Joystick1.Button9
Var.RThumb = Joystick1.Button10
else
var.Vibrate1 = XInput1.Vibration1
var.Vibrate2 = XInput1.Vibration2
var.RB = Xinput1.RightShoulder
Var.RT = (Xinput1.RightTrigger > var.TriggerDeadzone)
Var.LB = Xinput1.LeftShoulder
Var.LT = (Xinput1.LeftTrigger > var.TriggerDeadzone)
Var.X = Xinput1.X
Var.Y = Xinput1.Y
Var.A = Xinput1.A
Var.B = Xinput1.B
Var.Start = Xinput1.Start
Var.Back = Xinput1.Back
Var.DUp = Xinput1.Up
Var.DDown = Xinput1.Down
Var.DLeft = Xinput1.Left
Var.DRight = Xinput1.Right
Var.LThumb = Xinput1.LeftThumb
Var.RThumb = Xinput1.RightThumb
endif



//these keys are global and dont change with modifiers
Key.q = (-1.2 < var.MoveX < -var.MoveDeadzone)     //strafe left is left stick left
Key.e = (var.MoveDeadzone < var.MoveX < 1.2)       //strafe right is left stick right

//right analog stick mouse
if (var.MouseX > var.MouseDeadzone) mouse.cursorPosx += var.MouseSensitivity^(var.MouseX)     //cursor right
if (var.MouseX < -var.MouseDeadzone) mouse.cursorPosx -= -var.MouseSensitivity^1*(var.MouseX) //cursor left
if (var.MouseY > var.MouseDeadzone) mouse.cursorPosy += var.MouseSensitivity^(var.MouseY)         //cursor down
if (var.MouseY < -var.MouseDeadzone) mouse.cursorPosy -= -var.MouseSensitivity^1*(var.MouseY)     //cursor up

//vibrate when action bar 1 is activated so we know without looking
if pressed(Var.X) && (Var.RT == 1)
var.Vibrate2 = .35
var.Vibrate1 = .35
wait 150ms
var.Vibrate2 = 0
var.Vibrate1 = 0
endif

if Var.RT&&Var.RB then
Key.Spacebar = 1
elseif released(Var.RB)
Key.Spacebar = 0
endif

//left shoulders mouse buttons
if Mouse.RightButton==false&&pressed(var.LB) then
if Var.RT then press(shift)
press(Mouse.RightButton)
elseif Mouse.RightButton==true&&var.LB then
if Var.RT then press(shift)
press(Mouse.RightButton)
elseif Mouse.RightButton==true&&released(var.LB) then
if Var.RT then release(shift)
release(Mouse.RightButton)
endif

if Mouse.LeftButton==false&&Var.LT then
if Var.RT then press(shift)
press(Mouse.LeftButton)
elseif Mouse.LeftButton==true&&var.LT then
if Var.RT then press(shift)
press(Mouse.LeftButton)
elseif Mouse.LeftButton==true&&released(var.LT) then
if Var.RT then release(shift)
release(Mouse.LeftButton)
endif

if key.w==0 and var.MoveY
if var.RB
Mouse.Button4 = (-1.2 < var.MoveY < -var.MoveDeadzone)
else
key.w = (-1.2 < var.MoveY < -var.MoveDeadzone)
endif
elseif pressed(var.MoveY>-var.MoveDeadzone)
release(Mouse.Button4)
release(key.w)
endif

if key.s==0 and var.MoveY
if var.RB
key.Apostrophe = (var.MoveDeadzone < var.MoveY < 1.2)
else
key.s = (var.MoveDeadzone < var.MoveY < 1.2)
endif
elseif pressed(var.MoveY<var.MoveDeadzone)
release(key.Apostrophe)
release(key.s)
endif



if Var.RB==0
//no modifier button assignment 1-0
if Var.RT==0
Key.One = Var.X
Key.Two = Var.Y
Key.Three = Var.A 
Key.Four = Var.B
Key.Five = Var.Start
Key.Six = Var.Back
Key.Seven = Var.DUp
Key.Eight = Var.DDown
Key.Nine = Var.DRight
Key.Zero = Var.DLeft
else
Shift+One = pressed(Var.X)
Shift+Two = pressed(Var.Y)
Shift+Three = pressed(Var.A )
Shift+Four = pressed(Var.B)
Shift+Five = pressed(Var.Start)
Shift+Six = pressed(Var.Back)
Shift+Seven = pressed(Var.DUp)
Shift+Eight = pressed(Var.DDown)
Shift+Nine = pressed(Var.DRight)
Shift+Zero = pressed(Var.DLeft)
endif
//left analog stick forward and backward

//shift button modifier assignment
if Var.RT==0
Key.Minus = Var.LThumb
Key.Equals = Var.RThumb
else
Mouse.WheelUp = pressed(Var.LThumb)+Var.RT
Mouse.WheelDown = pressed(Var.RThumb)+Var.RT
endif
else
//xbox shoulder button modifier
Ctrl+One = pressed(var.X)
Ctrl+Two = pressed(var.Y)
Ctrl+Three = pressed(var.A)
Ctrl+Four = pressed(var.B)
Ctrl+Five = pressed(var.Start)
Tab = var.Back
Ctrl+Six = pressed(var.DUp)
Ctrl+Seven = pressed(var.DDown)
Ctrl+Eight = pressed(var.DRight)
Ctrl+Nine = pressed(var.DLeft)
Ctrl+Zero = pressed(var.LThumb)
if pressed(var.RThumb) then Toggle(Key.T)
endif

