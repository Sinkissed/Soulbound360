//Soulbound360
//by Sinkissed
//0.39
//For DirectX Driver
//Specific Target Control Set
//Key Bindings are Default

//useful user-changeable values
var.InvertMouse = false
var.SwapJoysticks = false
var.MouseSensitivity = screen.width/32
//end useful user-changeable values

//mess with lines below at your own risk!
if Xinput.Exists==0 then
if var.SwapJoysticks == false then
var.MouseX = Joystick1.pitch
var.MouseY = Joystick1.yaw
var.MoveX = Joystick1.x
var.MoveY = Joystick1.y
else
var.MoveX = Joystick1.pitch
var.MoveY = Joystick1.yaw
var.MouseX = Joystick1.x
var.MouseY = Joystick1.y
endif
var.MouseDeadzone = .07
var.MoveDeadzone = .05
else
if var.SwapJoysticks == false then
var.MouseX = Xinput1.Joy2x
var.MouseY = -Xinput1.Joy2y
var.MoveX = Xinput1.Joy1x
var.MoveY = -Xinput1.Joy1y
else
var.MoveX = Xinput1.Joy2x
var.MoveY = -Xinput1.Joy2y
var.MouseX = Xinput1.Joy1x
var.MouseY = -Xinput1.Joy1y
endif
var.MouseDeadzone = .18
var.MoveDeadzone = .18
var.TriggerDeadzone = 0.85
endif

if Xinput.Exists==0 then
var.Vibrate1 = Joystick1.Vibration1
var.Vibrate2 = Joystick1.Vibration2
var.RB = Joystick1.Button6
Var.RT = Joystick1.Button12
Var.LB = Joystick1.Button5
Var.LT = Joystick1.Button11
Var.X = Joystick1.Button3
Var.Y = Joystick1.Button4
Var.A = Joystick1.Button1
Var.B = Joystick1.Button2
Var.Start = Joystick1.Button8
Var.Back = Joystick1.Button7
Var.DUp = Joystick1.Pov1Up
Var.DDown = Joystick1.Pov1Down
Var.DLeft = Joystick1.Pov1Left
Var.DRight = Joystick1.Pov1Right
Var.LThumb = Joystick1.Button9
Var.RThumb = Joystick1.Button10
else
var.Vibrate1 = XInput1.Vibration1
var.Vibrate2 = XInput1.Vibration2
var.RB = Xinput1.RightShoulder
Var.RT = (Xinput1.RightTrigger > var.TriggerDeadzone)
Var.LB = Xinput1.LeftShoulder
Var.LT = (Xinput1.LeftTrigger > var.TriggerDeadzone)
Var.X = Xinput1.X
Var.Y = Xinput1.Y
Var.A = Xinput1.A
Var.B = Xinput1.B
Var.Start = Xinput1.Start
Var.Back = Xinput1.Back
Var.DUp = Xinput1.Up
Var.DDown = Xinput1.Down
Var.DLeft = Xinput1.Left
Var.DRight = Xinput1.Right
Var.LThumb = Xinput1.LeftThumb
Var.RThumb = Xinput1.RightThumb
endif


//these keys are global and dont change with modifiers
Key.q = (-1.2 < var.MoveX < -var.MoveDeadzone)     //strafe left is left stick left
Key.e = (var.MoveDeadzone < var.MoveX < 1.2)       //strafe right is left stick right

//mouse
if (var.MouseX > var.MouseDeadZone) mouse.cursorPosx += var.MouseSensitivity^(DeadZone(Var.MouseX, var.MouseDeadZone))     //cursor right
if (var.MouseX < -var.MouseDeadzone) mouse.cursorPosx -= -var.MouseSensitivity^1*(DeadZone(Var.MouseX, var.MouseDeadZone)) //cursor left
if var.InvertMouse == false then
if (var.MouseY  > var.MouseDeadZone) mouse.cursorPosy += var.MouseSensitivity^(DeadZone(Var.MouseY, var.MouseDeadZone))         //cursor down
if (var.MouseY  < -var.MouseDeadZone) mouse.cursorPosy -= -var.MouseSensitivity^1*(DeadZone(Var.MouseY, var.MouseDeadZone))     //cursor up
else
if (var.MouseY  < -var.MouseDeadZone) mouse.cursorPosy += var.MouseSensitivity^(-1*DeadZone(Var.MouseY, var.MouseDeadZone) )         //cursor down
if (var.MouseY  > var.MouseDeadZone) mouse.cursorPosy -= var.MouseSensitivity^(DeadZone(Var.MouseY, var.MouseDeadZone))         //cursor down
endif

//vibrate when action bar 1 is activated so we know without looking
if pressed(Var.X) && (Var.RT == 1)
var.Vibrate2 = .35
var.Vibrate1 = .35
wait 150ms
var.Vibrate2 = 0
var.Vibrate1 = 0
endif

if Var.RT&&Var.RB then
shift = 0
Key.Spacebar = 1
elseif released(Var.RB) or released(Var.RT)
Key.Spacebar = 0
endif

//left shoulders mouse buttons
if Mouse.RightButton==false&&pressed(var.LB) then
press(Mouse.RightButton)
elseif Mouse.RightButton==true&&var.LB then
Mouse.RightButton=1
elseif Mouse.RightButton==true&&released(var.LB) then
if Var.RT then press(shift)
release(Mouse.RightButton)
shift = 0
endif

if Mouse.LeftButton==false&&pressed(Var.LT) then
press(Mouse.LeftButton)
elseif Mouse.LeftButton==true&&var.LT then
Mouse.LeftButton=1
elseif Mouse.LeftButton==true&&released(var.LT) then
if Var.RT then press(shift)
release(Mouse.LeftButton)
shift = 0
endif

//left analog stick forward,backward,autorun,follow
if var.RB then
Mouse.Button4 = (-1.2 < var.MoveY < -var.MoveDeadzone)
endif

if key.w==0 and var.MoveY && var.RB==0 then
key.w = (-1.2 < var.MoveY < -var.MoveDeadzone)
elseif pressed(var.MoveY>-var.MoveDeadzone)
release(Mouse.Button4)
release(key.w)
endif

if var.RB then
key.Apostrophe = (var.MoveDeadzone < var.MoveY < 1.2)
endif

if key.s==0 and var.MoveY && var.RB==0 then
key.s = (var.MoveDeadzone < var.MoveY < 1.2)
elseif pressed(var.MoveY<var.MoveDeadzone)
release(key.Apostrophe)
release(key.s)
endif

if released(rightshift)
shift = 0
endif

if Var.RB==0
//no modifier button assignment 1-0
if Var.RT==0
if released(var.RT)
shift = 0
elseif leftshift or rightshift then
shift = 1
else
shift = 0
endif
Key.One = Var.X
Key.Two = Var.Y
Key.Three = Var.A 
Key.Four = Var.B
Key.Five = Var.Start
Key.Six = Var.Back
Key.Seven = Var.DUp
Key.Eight = Var.DDown
Key.Nine = Var.DRight
Key.Zero = Var.DLeft
Key.Minus = Var.LThumb
Key.Equals = Var.RThumb
else
//shift button modifier assignment
if ((not var.x) and (not var.y) and (not var.a) and (not var.b) and (not var.dup) and (not var.ddown) and (not var.dleft) and (not var.dright) and (not var.start) and (not var.back) and (not var.rb) and (not var.lb) and (not var.lt) and (not var.rthumb) and (not var.lthumb)) then
shift = 1
else
shift = 0
Shift+One = pressed(Var.X)
Shift+Two = pressed(Var.Y)
Shift+Three = pressed(Var.A )
Shift+Four = pressed(Var.B)
Shift+Five = pressed(Var.Start)
Shift+Six = pressed(Var.Back)
Shift+Seven = pressed(Var.DUp)
Shift+Eight = pressed(Var.DDown)
Shift+Nine = pressed(Var.DRight)
Shift+Zero = pressed(Var.DLeft)
Mouse.WheelUp = pressed(Var.LThumb)+Var.RT
Mouse.WheelDown = pressed(Var.RThumb)+Var.RT
endif
endif
else
//right shoulder button modifier
Key.F1 = Var.Start
Key.F2 = Var.X
Key.F3 = Var.Y
Key.F4 = Var.A 
Key.F5 = Var.B
Key.F = pressed(Var.LThumb)
Tab = pressed(Var.Back)
Shift+Tab = pressed(Var.DUp)
CtrL+Comma =  pressed(Var.DDown)
Ctrl+T =  pressed(Var.DRight)
Ctrl+Shift+T =  pressed(Var.DLeft)
if pressed(var.RThumb) then press(Key.T)
if released(var.RThumb) then release(Key.T)
endif
