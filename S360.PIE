//Soulbound360
//by Sinkissed
//0.61

//Setting this variable to true allows you to heal or attack a
//target by pressing any spell while hovering over a target.
//Its like clique addon and its extremely useful for healing.
var.HoverAction=false

//This is the order of the action bars when the trigger is pressed.
//default is 1 2 3 4 5 6
shift+key.1+release(shift) = var.BarOff
shift+key.2+release(shift) = var.BarFull
shift+key.3+release(shift) = var.BarHalf
shift+key.4+release(shift) = var.BarHalfModifiedFull
shift+key.5+release(shift) = var.BarFullModifiedFull
shift+key.6+release(shift) = var.BarHalfModifiedHalf

//PushToTalkKey is your Ventrilo or Teamspeak push to talk key.
//When triggered from the gamepad it stays on till pressed again.
key.NUMPAD6 = var.PushToTalkKey

//SwapJoysticks makes joystick functions switch sides.
//Use if you are left handed.
var.SwapJoysticks = false

//SwapShoulders makes shoulder functions switch sides.
//Use if you are left-handed.
var.SwapShoulders = false

//A lower value here will make the mouse move faster.
var.MouseSensitivity = screen.width/32

//InvertMouse flips the mouse's Y/vertical axis
var.InvertMouse = false

//SeparateClicks moves left click to the top right shoulder
//instead of under a modifier. If false, Right Click MUST
//be relased before left click can be pressed. If true,
//Left Click has a dedicated butting allowing simpler healing
//and left/right together to run. This will move target so that
//it is under the modifier for Left Click. Jumping with this
//setting will end autorun unless the modifier is also held.
var.SeparateClicks = false

//SwapMouseButtons switches the position of the mouse click button.
//One click can only be available with a modifier.
//This is an alternate way to free up Left Click for healing.
//It makes moving harder though.
var.SwapMouseButtons = false

//JOYSTICK ALIAS
//case check for driver
if Xinput.Exists==0 then
   if var.SwapJoysticks == false then
      var.MouseX = Joystick1.pitch
      var.MouseY = Joystick1.yaw
      var.MoveX = Joystick1.x
      var.MoveY = Joystick1.y
   else
      var.MoveX = Joystick1.pitch
      var.MoveY = Joystick1.yaw
      var.MouseX = Joystick1.x
      var.MouseY = Joystick1.y
   endif
   var.MouseDeadzone = .07
   var.MoveDeadzone = .05
   var.TriggerDeadzone = 0.05
else
    if var.SwapJoysticks == false then
       var.MouseX = Xinput1.Joy2x
       var.MouseY = -Xinput1.Joy2y
       var.MoveX = Xinput1.Joy1x
       var.MoveY = -Xinput1.Joy1y
    else
       var.MoveX = Xinput1.Joy2x
       var.MoveY = -Xinput1.Joy2y
       var.MouseX = Xinput1.Joy1x
       var.MouseY = -Xinput1.Joy1y
    endif
    var.MouseDeadzone = .185
    var.MoveDeadzone = .18
    var.TriggerDeadzone = 0.25
endif

//Trigger sensitivity
var.SwitchDeadzone = .25

//BUTTON ALIAS
//case check for driver
if Xinput.Exists==0 then
   if var.SwapShoulders = false then
      Var.RB = Joystick1.Button6
      Var.RT = Joystick1.z>Var.TriggerDeadzone
      Var.LB = Joystick1.Button5
      Var.LT = Joystick1.roll>var.TriggerDeadzone
   else
      Var.LB = Joystick1.Button6
      Var.LT = Joystick1.z>Var.TriggerDeadzone
      Var.RB = Joystick1.Button5
      Var.RT = Joystick1.roll>var.TriggerDeadzone
   endif
   Var.X = Joystick1.Button3
   Var.Y = Joystick1.Button4
   Var.A = Joystick1.Button1
   Var.B = Joystick1.Button2
   Var.Start = Joystick1.Button8
   Var.Back = Joystick1.Button7
   Var.DUp = Joystick1.Pov1Up
   Var.DDown = Joystick1.Pov1Down
   Var.DLeft = Joystick1.Pov1Left
   Var.DRight = Joystick1.Pov1Right
   if var.SwapJoysticks == false then
      Var.LThumb = Joystick1.Button9
      Var.RThumb = Joystick1.Button10
   else
       Var.RThumb = Joystick1.Button9
       Var.LThumb = Joystick1.Button10
   endif
   Var.Bar1 = (Joystick1.z < var.SwitchDeadzone)
   Var.Bar2 = (var.SwitchDeadzone < Joystick1.z < 1)
   Var.Bar3 = (1 == Joystick1.z)
   Var.Type1 = (Joystick1.roll < var.SwitchDeadzone)
   Var.Type2 = (var.SwitchDeadzone < Joystick1.roll < 1)
   Var.Type3 = (1 == Joystick1.roll)
   else
   if var.SwapShoulders = false then
      Var.RB = Xinput1.RightShoulder
      Var.RT = (Xinput1.RightTrigger > var.TriggerDeadzone)
      Var.LB = Xinput1.LeftShoulder
      Var.LT = (Xinput1.LeftTrigger > var.TriggerDeadzone)
      else
      Var.LB = Xinput1.RightShoulder
      Var.LT = (Xinput1.RightTrigger > var.TriggerDeadzone)
      Var.RB =  Xinput1.LeftShoulder
      Var.RT = (Xinput1.LeftTrigger > var.TriggerDeadzone)
   endif
   Var.X = Xinput1.X
   Var.Y = Xinput1.Y
   Var.A = Xinput1.A
   Var.B = Xinput1.B
   Var.Start = Xinput1.Start
   Var.Back = Xinput1.Back
   Var.DUp = Xinput1.Up
   Var.DDown = Xinput1.Down
   Var.DLeft = Xinput1.Left
   Var.DRight = Xinput1.Right
   if var.SwapJoysticks == false then
      Var.LThumb = Xinput1.LeftThumb
      Var.RThumb = Xinput1.RightThumb
   else
      Var.RThumb = Xinput1.LeftThumb
      Var.LThumb = Xinput1.RightThumb
   endif
   Var.Bar1 = (Xinput1.RightTrigger < var.SwitchDeadzone)
   Var.Bar2 = (var.SwitchDeadzone < Xinput1.RightTrigger < 1)
   Var.Bar3 = (1 == Xinput1.RightTrigger)
   Var.Type1 = (Xinput1.LeftTrigger < var.SwitchDeadzone)
   Var.Type2 = (var.SwitchDeadzone < Xinput1.LeftTrigger < 1)
   Var.Type3 = (1 == Xinput1.LeftTrigger)
endif

//MOUSE BINDINGS
if (var.MouseX > var.MouseDeadZone) mouse.cursorPosx += var.MouseSensitivity^(DeadZone(Var.MouseX, var.MouseDeadZone))     //cursor right
if (var.MouseX < -var.MouseDeadzone) mouse.cursorPosx -= -var.MouseSensitivity^1*(DeadZone(Var.MouseX, var.MouseDeadZone)) //cursor left
if var.InvertMouse == false then
   if (var.MouseY  > var.MouseDeadZone) mouse.cursorPosy += var.MouseSensitivity^(DeadZone(Var.MouseY, var.MouseDeadZone))         //cursor down
   if (var.MouseY  < -var.MouseDeadZone) mouse.cursorPosy -= -var.MouseSensitivity^1*(DeadZone(Var.MouseY, var.MouseDeadZone))     //cursor up
else
   if (var.MouseY  < -var.MouseDeadZone) mouse.cursorPosy += var.MouseSensitivity^(-1*DeadZone(Var.MouseY, var.MouseDeadZone) )         //cursor down
   if (var.MouseY  > var.MouseDeadZone) mouse.cursorPosy -= var.MouseSensitivity^(DeadZone(Var.MouseY, var.MouseDeadZone))         //cursor down
endif

//move
release(ctrl)+Key.q = (-1.2 < var.MoveX < -0.5)
release(ctrl)+Key.e = (0.5 < var.MoveX < 1.2)
release(ctrl)+key.w = (-1.2 < var.MoveY < -var.MoveDeadzone)
release(ctrl)+key.s = (var.MoveDeadzone < var.MoveY < 1.2)

//jump
key.Spacebar = Var.RB&&Var.RT

//autorun + follow
if Var.LB and doubleclicked(var.LT) then
  if (not var.bar3) and (not var.bar2) then
     press(Mouse.Button4)
     release(Mouse.Button4)
  else
     press(key.Apostrophe)
     release(key.Apostrophe)
  endif
endif

//CLICK BINDINGS
if var.SeparateClicks == false
if var.SwapMouseButtons == true then
   //right mouse button BELOW
   if Mouse.RightButton==false&&var.LB&&var.LT==true then
      if Var.LB then release(Mouse.LeftButton)
         press(Mouse.RightButton)
      elseif Mouse.RightButton==true&&var.LB then
         Mouse.RightButton=1
      elseif Mouse.RightButton==true&&released(var.LB) then
         if var.Bar3 then press(shift)
         release(Mouse.RightButton)
         release(shift)
   endif
   //left mouse button ABOVE
   if Mouse.LeftButton==false&&var.LB&&var.LT==false&&Mouse.RightButton==false then
      press(Mouse.LeftButton)
   elseif Mouse.LeftButton==true&&var.LB then
      Mouse.LeftButton=1
   elseif Mouse.LeftButton==true&&released(var.LB) then
      if var.Bar3 then press(shift)
      if var.Bar2 then press(ctrl)
      release(Mouse.LeftButton)
      release(ctrl)
      release(shift)
   endif
else
   //right mouse button ABOVE
   if Mouse.RightButton==false&&var.LB&&var.LT==false then
      if Mouse.LeftButton then release(Mouse.LeftButton)
      Mouse.RightButton=1
   elseif Mouse.RightButton==true&&var.LB then
      if Mouse.LeftButton then release(Mouse.LeftButton)
         Mouse.RightButton=1
      elseif Mouse.RightButton==true&&released(var.LB) then
      if var.Bar3 then press(shift)
      release(Mouse.RightButton)
      release(shift)
   endif
  //left mouse button BELOW
   if Mouse.LeftButton==false&&var.LB&&var.LT==true&&Mouse.RightButton==false then
      press(Mouse.LeftButton)
   elseif Mouse.LeftButton==true&&var.LB&&Mouse.RightButton==false then
      Mouse.LeftButton=1
   elseif Mouse.LeftButton==true&&(released(var.LB) or released(var.LT) or pressed(Mouse.RightButton))  then
      if var.Bar3 then press(shift)
      if var.Bar2 then press(ctrl)
      release(Mouse.LeftButton)
      release(ctrl)
      release(shift)
   endif
endif
else
if var.SwapMouseButtons == true then
   //left mouse button LEFT
   if Mouse.LeftButton==false&&var.LB&&var.LT==false then
      Mouse.LeftButton=1
   elseif Mouse.LeftButton==true&&var.LB then
         Mouse.LeftButton=1
   elseif Mouse.LeftButton==true&&released(var.LB) then
      if var.Bar3 then press(shift)
      if var.Bar2 then press(ctrl)
      release(Mouse.LeftButton)
      release(ctrl)
      release(shift)
   endif
   //right mouse button RIGHT
   if Mouse.RightButton==false&&var.RB==true&&var.Type3==false then
      press(Mouse.RightButton)
   elseif Mouse.RightButton==true&&var.RB&&var.Type3==false then
      Mouse.RightButton=1
   elseif Mouse.RightButton==true&&released(var.RB) then
      if var.Bar2 then press(shift)
      release(Mouse.RightButton)
      release(shift)
   endif
else
   //right mouse button LEFT
   if Mouse.RightButton==false&&var.LB&&var.LT==false then
      Mouse.RightButton=1
   elseif Mouse.RightButton==true&&var.LB then
         Mouse.RightButton=1
   elseif Mouse.RightButton==true&&released(var.LB) then
      if var.Bar3 then press(shift)
      release(Mouse.RightButton)
      release(shift)
   endif
   //left mouse button RIGHT
   if Mouse.LeftButton==false&&var.RB==true&&var.Type3==false then
      press(Mouse.LeftButton)
   elseif Mouse.LeftButton==true&&var.RB&&var.Type3==false then
      Mouse.LeftButton=1
   elseif Mouse.LeftButton==true&&released(var.RB) then
      if var.Bar2 then press(ctrl)
      release(Mouse.LeftButton)
      release(ctrl)
   endif
endif
endif

//KEYBOARD BINDINGS
if Var.Type1==true then
     if (var.HoverAction==true) and (pressed(Var.X) or pressed(Var.Y) or pressed(Var.A) or pressed(Var.B) or pressed(Var.Back) or pressed(Var.Start) or pressed(Var.DDown) or pressed(Var.DUp) or pressed(Var.DLeft) or pressed(Var.DRight) or pressed(Var.LThumb) or pressed(Var.RThumb)) then
        press(Key.Dot)
        release(Key.Dot)
     endif
     Key.One = pressed(Var.X)
     Key.Two = pressed(Var.Y)
     Key.Three = pressed(Var.A)
     Key.Four = pressed(Var.B)
     Key.Five = pressed(Var.Start)
     Key.Six = pressed(Var.Back)
     Key.Seven = pressed(Var.DUp)
     Key.Eight = pressed(Var.DDown)
     Key.Nine = pressed(Var.DRight)
     Key.Zero = pressed(Var.DLeft)
     Key.Minus = pressed(Var.LThumb)
     Key.Equals = pressed(Var.RThumb)
     if var.RT=0&&var.SeparateClicks == false then Key.Tab = pressed(var.RB)
     var.BarOff = pressed(var.Bar1)
     var.BarFull = pressed(var.Bar3)
     var.BarHalf = pressed(var.Bar2)
elseif var.Type2==true then
     Ctrl+One = pressed(Var.X)
     Ctrl+Two = pressed(Var.Y)
     Ctrl+Three =  pressed(Var.A)
     Ctrl+Four =  pressed(Var.B)
     Ctrl+Five =  pressed(Var.Start) and not Var.Bar3
     Ctrl+Six = pressed(Var.Back) and not Var.Bar3
     Ctrl+Seven = pressed(Var.DUp) and not Var.Bar3 and not Var.Bar2
     Ctrl+Eight = pressed(Var.DDown) and not Var.Bar3 and not Var.Bar2
     Ctrl+Nine = pressed(Var.DRight) and not Var.Bar3 and not Var.Bar2
     Ctrl+Zero = pressed(Var.DLeft) and not Var.Bar3 and not Var.Bar2
     key.i = pressed(Var.Start) and Var.Bar3
     key.l = pressed(Var.Back) and Var.Bar3
     key.c = pressed(Var.DUp) and Var.Bar2
     key.p = pressed(Var.DUp) and Var.Bar3
     key.b = pressed(Var.DDown) and Var.Bar2
     key.n = pressed(Var.DDown) and Var.Bar3
     key.h = pressed(Var.DRight) and Var.Bar2
     key.y = pressed(Var.DRight) and Var.Bar3
     key.j = pressed(Var.DLeft) and Var.Bar2
     key.o = pressed(Var.DLeft) and Var.Bar3
     if var.bar3 then
        ctrl+divide = pressed(Var.Lthumb)
        ctrl+numpadplus = pressed(Var.Rthumb)
     elseif var.bar2 then
        ctrl+numpadminus = pressed(Var.Lthumb)
        ctrl+multiply = pressed(Var.Rthumb)
     else
        ctrl+shift+t = pressed(Var.Lthumb)
        ctrl+t = pressed(Var.Rthumb)
     endif
     var.BarHalfModifiedHalf = var.Bar2 && pressed(var.type2) and not released(var.type3)
elseif var.Type3==true then
     Key.F1 = pressed(Var.Start) and not Var.Bar2 and not Var.Bar3
     Key.F2 = pressed(Var.X) and not Var.Bar3
     Key.F3 = pressed(Var.Y) and not Var.Bar3
     Key.F4 = pressed(Var.A) and not Var.Bar3
     Key.F5 = pressed(Var.B) and not Var.Bar3
     Ctrl+F1 = pressed(Var.Back) and not Var.Bar3
     Ctrl+F2 = pressed(Var.DUp) and not Var.Bar3
     Ctrl+F3 = pressed(Var.DDown) and not Var.Bar3
     Ctrl+F4 = pressed(Var.DRight) and not Var.Bar3
     Ctrl+F5 = pressed(Var.DLeft) and not Var.Bar3
     Key.F = pressed(Var.Start) and Var.Bar2
     Key.T = pressed(Var.Start) and Var.Bar3
     Key.NUMPAD7 = pressed(Var.X) and Var.Bar3
     Key.NUMPAD8 = pressed(Var.Y) and Var.Bar3
     Key.NUMPAD9 = pressed(Var.A) and Var.Bar3
     Key.NUMPAD2 = pressed(Var.B) and Var.Bar3
     Ctrl+F6 = pressed(Var.Back) and Var.Bar3
     Ctrl+F7 = pressed(Var.DUp)  and Var.Bar3
     Ctrl+F8 = pressed(Var.DDown) and Var.Bar3
     Ctrl+F9 = pressed(Var.DRight) and Var.Bar3
     Ctrl+F10 = pressed(Var.DLeft) and Var.Bar3
     if var.RT=0&&var.SeparateClicks == false then Shift+Tab = pressed(var.RB)
     if var.RT=0&&var.SeparateClicks == true then Tab = pressed(var.RB)
     if var.bar2 then
        key.m = pressed(Var.Lthumb)
        shift = Var.Rthumb
     elseif var.bar3 then
        if var.Lthumb then
           wait 5ms
           Mouse.WheelUp = 1
           wait 5ms
           Mouse.WheelUp = 0
        elseif released(var.Lthumb)
           Mouse.WheelUp = 0
        else
           Mouse.WheelUp = 0
        endif
        if var.Rthumb then
           wait 5ms
           Mouse.WheelDown = 1
           wait 5ms
           Mouse.WheelDown = 0
        elseif released(var.Rthumb)
           Mouse.WheelDown = 0
        else
           Mouse.WheelDown = 0
        endif
     else
        key.esc = pressed(Var.Lthumb)
        if pressed(var.Rthumb)&&var.TogglePushToTalk==0 then
           press(key.ScrollLock)
           release(key.ScrollLock)
           var.TogglePushToTalk = 1
        elseif pressed(var.Rthumb)&&var.TogglePushToTalk==1 then
           press(key.ScrollLock)
           release(key.ScrollLock)
           var.TogglePushToTalk = 0
        endif
     endif
     var.BarHalfModifiedFull = var.Bar2 && pressed(var.type3)
     var.BarFullModifiedFull = var.Bar3 && pressed(var.type3)
endif

if pressed(HeldDown(Var.Start,3)) then
   enter = 1
   enter = 0
   slash = 1
   slash = 0
   key.s=1
   key.s=0
   type(360)
   enter = 1
   enter = 0
endif

if pressed(HeldDown(Var.Back,3)) then
   key.escape=1
   key.escape=0
endif

if var.TogglePushToTalk == 1 then
   var.PushToTalkKey = true
elseif var.TogglePushToTalk == 0 then
   var.PushToTalkKey = false
endif

